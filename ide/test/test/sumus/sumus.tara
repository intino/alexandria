dsl Konos

Activity Platform-Web
	Home(uses = analytics, paths = "/analytics") analyticsPage
	Page(uses = error, paths = "/error") errorPage

	Display analytics
		displays = olap dashboard pivotTable

		Notification info as Object(PlatformInfo)
		Notification boardList as Object(BoardList)
		Notification user as Object(UserInfo)
		Notification userLoggedOut
		Notification(Client) loading
		Notification(Client) loaded

		Request logout

	Display olap
		Notification timeline as Object(Timeline)
		Notification timeRange as Object(Range)
		Notification scales as Object(ScaleList)
		Notification updateScale as Object(Scale)
		Notification enableDrill
		Notification disableDrill
		Notification enableFilter
		Notification disableFilter
		Notification filtering
		Notification notFiltering
		Notification drilling
		Notification notDrilling
		Notification filterList as Object(FilterList)
		Notification ticketList as Object(NameList)
		Notification selectedTicketList as Object(NameList)

		Request addTicket as Text
		Request removeTicket as Text
		Request updateRange as Object(RequestRange)
		Request(Asset) export as Object(RequestRange)
		Request toggleDrill
		Request toggleFilter
		Request removeFilter as Text

	Display timeSeries
		Notification zoomRange as Object(Range)
		Notification timeRange as Object(Range)
		Notification timeline as Object(Timeline)
		Notification formats as Object(FormatterList)
		Notification updateScale as Object(Scale)
		Notification(Client) loading
		Notification(Client) loaded
		Notification clear
		Notification eventListCount as Real
		Notification eventList as Object(EventList)
		Notification addSeries as Object(SerieList)
		Notification removeSeries as Object(NameList)
		Notification addPoints as Object(Points)

		Request updateRange as Object(RequestRange)
		Request updateRangeAndScale as Object(RequestRange)
		Request move as Object(RequestRange)

	Display analyze
		displays = categorization
		Notification categorizationList as Object(CategorizationViewList)
		Notification hide
		Notification show

	Display categorization
		Notification categorization as Object(CategorizationView)
		Notification enableSelection
		Notification disableSelection
		Notification(Client) loading
		Notification(Client) loaded
		Notification refreshHistogram as Object(Histogram)
		Notification refreshSelected as Object(CategoryViewList)

		Request toggle as Object(RequestCategory)
		Request refresh

	Display dashboard
	Display pivotTable
	Display error

Schema PlatformInfo
	Attribute title as Text
	Attribute subtitle as Text
	Attribute logo as Text

Schema BoardList
	Schema Board
		Attribute label as Text

Schema UserInfo
	Attribute fullName as Text
	Attribute photo as Text

Schema Timeline
	Attribute min as Real
	Attribute max as Real

Schema RequestRange
	Attribute from as Real
	Attribute to as Real

Schema Range
	Attribute min as Real
	Attribute max as Real

Schema ScaleList
	Has(Scale)


Schema Alert
    Attribute active as Bool
    Attribute(multiple=true) mailingList as Text

Schema Scale
	Attribute name as LongInteger
	Attribute label as Text
	Attribute symbol as Text

Schema FilterList
	Schema Filter
		Schema(multiple=false) Categorization
			Attribute name as Text
			Attribute label as Text
		Schema Category
			Attribute name as Text
			Attribute label as Text

Schema CategorizationViewList
	Has(CategorizationView)

Schema CategorizationView
	Attribute name as Text
	Attribute label as Text

	Has(CategoryView)
	Has(CategorizationView, multiple=false) subCategorization

Schema CategoryViewList
	Has(CategoryView)

Schema CategoryView
	Attribute name as Text
	Attribute label as Text
	Has(CategoryView)

Schema FormatterList
	Schema Formatter
		Attribute name as Text
		Attribute format as Text

Schema EventList
	Schema Event
		Attribute label as Text
		Attribute Instant as Real
		Attribute render as Text
		Attribute icon as Text

Schema SerieList
	Has(Serie)

Schema Serie
	Attribute label as Text
	Attribute group as Text
	Attribute metric as Text
	Attribute color as Text
	Attribute axisColor as Text
	Attribute style as Text
	Attribute min as Integer
	Attribute max as Integer

	Has(DecimalPlaces, multiple=false)
	Schema data
		Attribute Instant as Real
		Attribute value as Real

Schema DecimalPlaces
	Attribute absolute as Integer
	Attribute percentage as Integer

Schema Points
	Has(SerieList, multiple=false)
	Has(Scale, multiple=false)

Schema NameList
	Attribute(multiple=true) names as Text

Schema Histogram
	Has(CategoryView)
	Has(Ticket)

Schema Ticket
	Attribute label as Text
	Attribute(multiple=true) values as Real

	Schema Indicator
		Attribute label as Text
		Attribute color as Text
		Attribute(multiple=true) values as Real

		Has(DecimalPlaces, multiple=false)

Schema RequestCategory
	Attribute name as Text
	Attribute categorization as Text






