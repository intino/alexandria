dsl Proteo

use DataTypes

Concept Client

Facet Ness on Client
	var string url
	var string user
	var string password
	var word:{Normal Transacted} mode = Normal
	var string domain
	var string clientID
	has Mounter
	has Feeder

Concept:NamedIfNoSchema Mounter extends EventHandler is component

Concept:Named Feeder is component
	var string subdomain = ""
	var Schema[] eventTypes
	Concept:NamedIfNoSchema Sensor

Facet FormEdition on Feeder.Sensor
	var string path

Facet DocumentEdition on Feeder.Sensor
	var word:{Offline Online} mode = Online

Facet DocumentSignature on Feeder.Sensor
	var word:{Sign CoSign CounterSign} signType
	var word:{Pkcs7 XadesAttached XadesDetached CadesAttached CadesDetached} signFormat

Facet Poll on Feeder.Sensor
	var string defaultOption = empty
	Concept Option
		var string value
		has Option

Facet Device on Feeder.Sensor

//Facet Scrapper on Feeder.Sensor

Concept EventHandler is abstract
	var string name = 'self.schema()!= null ? self.schema().name$(): self.name$()' is private
	var Schema schema
	var string subdomain = ""
