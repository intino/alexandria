dsl Proteo

use Source
use Facets
use Display
use DataTypes
use UI

Concept:Named Component extends Display
	var string color = empty
	var Format[] format = empty

Concept Block extends Component
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None

	has Component

Concept:Named Template extends Component
	var string modelClass = empty
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None

	has Component

Concept:Named Catalog extends Component
	var Datasource source

	Concept:{0..1} Events
		Concept:{1..1} OnClick
			Concept:{1..1} CatalogEvent
				sub Open
					var Block block
					var boolean hasBreadcrumbs = false
				sub OpenDialog
					var Dialog dialog
				sub OpenCatalog
					var Catalog catalog
					var boolean filtered = false
					var boolean openItem = false

	has Component


//Concept DataComponents
//Concept CatalogComponents
//Concept OperationComponents
//Concept OtherComponents

Concept ChildComponents

	Concept Chip extends Component
	Concept Card extends Component
	Concept Tooltip extends Component
	Concept User extends Component
	Concept KPI extends Component
	Concept Spinner extends Component
		var word:{Bar Circle Hash Ring Rise} mode = Rise

	Concept Chart extends Component
		var Dataframe input
		var word:{Image Html} output = Html
		var string query

	Concept Text extends Component
		var word:{Uppercase Lowercase Capitalize Normal} mode = Normal
		!! The initial text to display in the box, if any
		var string value = empty
		var string prefix = empty
		var string suffix = empty

	Concept Stamp extends Component
		var Template template

	Concept File extends Component
		var string value = empty

	Concept Image extends Component
		var string value = empty
		var string defaultValue = empty

	Concept Icon extends Component
		var string alexandriaIcon

	Concept Number extends Component
		var double value = -1
		var string prefix = empty
		var string suffix = empty

	Concept Date extends Component
		var string pattern = "DD/MM/YYYY"
		var word:{FromNow ToNow None} mode = None
		var instant value = empty

	Concept Snackbar extends Component
		var word:{TopCenter TopRight BottomRight BottomCenter BottomLeft TopLeft} position = BottomCenter
		has Component
	Concept Progress extends Component
		Concept Color > var string text; var string background
	Concept Wizard extends Component
		Concept Step > has Component

	Concept Operation extends Component
		var string title
		var word:{Blank Self Parent Top} target = Self

		sub OpenPage > var Page page
		sub OpenCatalogOperation
		sub OpenDialogOperation
		sub OpenExternalDialogOperation
		sub ExportOperation
		sub ExportSelectionOperation
		sub DownloadOperation
		sub DownloadSelectionOperation
		sub TaskOperation
		sub TaskSelectionOperation
		sub PreviewOperation
		sub SearchOperation

	Concept Header extends Component
		var word:{Fixed Absolute Sticky Static Relative} position = Relative

		has Component

	Concept:Named GroupBox extends Component
		var boolean visible = true
		var Classified.Grouping grouping

	Concept:Named SortBox extends Component
		var boolean visible = true
		var Indexed.Sorting sorting

	Concept:Named SearchBox extends Component
		var Indexed source
		var string placeholder

	Concept Selector extends Component
		has Component

	Concept Frame extends Component
		var Display display

	Concept Collection extends Component
		var string sourceClass = empty
		var string itemClass = empty
		var string noItemsMessage = empty
		var integer pageSize = 20

		Concept:{1..*} Mold
			Concept:{0..1} Heading extends Component > has Component
			Concept:{1..1} Item extends Component > has Component
				var integer:{px} height

		sub Magazine
		sub List
		sub Grid
		sub Table
		sub Map
			Concept:{0..1} Center
				var double latitude
				var double longitude

			Concept:{1..1} Zoom
				var integer defaultZoom
				var integer min
				var integer max