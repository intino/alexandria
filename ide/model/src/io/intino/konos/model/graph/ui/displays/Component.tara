dsl Proteo

use Facets
use Display
use DataTypes
use UI

Concept:Named Component extends Display
	var word:{Primary Secondary} color = Primary
	var Theme.Style style = empty

Concept Block extends Component
	var string label = empty
	var string for = empty

	has Component

Concept Catalog extends Component
	var string targetClass
	var string noItemsMessage = empty

	Concept:{0..1} Events
		Concept:{1..1} OnClick
			Concept:{1..1} CatalogEvent
				sub Open
					var Block block
					var boolean hasBreadcrumbs = false
				sub OpenDialog
					var Dialog dialog
				sub OpenCatalog
					var Catalog catalog
					var boolean filtered = false
					var boolean openItem = false

	Concept:{1..1} Layout extends Component
		has:{0..*} ChildComponents.Content

Concept ChildComponents

	Concept Badge extends Component
	Concept Chip extends Component
	Concept Card extends Component
	Concept Tooltip extends Component
	Concept User extends Component
	Concept Chart extends Component
	Concept Text extends Component
		var word:Format format = Default
		var word:{Uppercase Lowercase Capitalize Normal} mode = Normal
		var string value = empty

	Concept File extends Component
	Concept Image extends Component
	Concept Icon extends Component
		var string alexandriaIcon
	Concept Number extends Component

	Concept Date extends Component
		var string format = "dd/MM/yyyy"

	Concept DateTime extends Component
		var string format = "dd/MM/yyyy HH:mm:ss"

	Concept Snackbar extends Component
		var word:{TopCenter TopRight BottomRight BottomCenter BottomLeft TopLeft} position = BottomCenter
		has Component
	Concept Progress extends Component
		Concept Color > var string text; var string background
	Concept Wizard extends Component
		Concept Step > has Component

	Concept Operation extends Component
		var string label

		sub OpenCatalogOperation
		sub OpenDialogOperation
		sub OpenExternalDialogOperation
		sub ExportOperation
		sub ExportSelectionOperation
		sub DownloadOperation
		sub DownloadSelectionOperation
		sub TaskOperation
		sub TaskSelectionOperation
		sub PreviewOperation
		sub SearchOperation

	Concept AppBar extends Component
		var word:{Fixed Absolute Sticky Static Relative} position = Relative

		has Component

	Concept Tabs extends Component
		has Tab

	Concept Tab extends Component
		var string label = empty
		Concept:{0..1} Header
		has:{1..1} Content

	Concept Panels extends Component
		has Panel

	Concept Panel extends Component
		has Component

	Concept Content extends Component
		has Component

	Concept:Named Grouping extends Component
		var string label

	Concept:Named Sorting extends Component
		var string label
		var boolean visible = true

	Concept Selector extends Component
		has Component