dsl Proteo

use Facets
use Display
use DataTypes
use UI

Concept:Named Component extends Display
	var string color = empty
	var Format[] format = empty

Concept Block extends Component
	var string label = empty
	var string for = empty
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None

	has Component

Concept Mold extends Component
	var string modelClass = empty
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None

	has Component

Concept Collection extends Component is decorable
	var string modelClass
	var string noItemsMessage = empty

Concept Catalog extends Component
	var string targetClass
	var string noItemsMessage = empty

	Concept:{0..1} Events
		Concept:{1..1} OnClick
			Concept:{1..1} CatalogEvent
				sub Open
					var Block block
					var boolean hasBreadcrumbs = false
				sub OpenDialog
					var Dialog dialog
				sub OpenCatalog
					var Catalog catalog
					var boolean filtered = false
					var boolean openItem = false

	has Component

Concept DataFrame
	Concept Column
		var string name
		var word:{String Integer Double Boolean} type

Facet CSV on DataFrame
	var string datasourceFilename
Facet Source on DataFrame
	var string datasourceClass

Concept ChildComponents

	Concept Chip extends Component
	Concept Card extends Component
	Concept Tooltip extends Component
	Concept User extends Component
	Concept KPI extends Component
	Concept Chart extends Component
		var DataFrame input
		var word:{Image Html} output = Html
		var string query

	Concept Text extends Component
		var word:{Uppercase Lowercase Capitalize Normal} mode = Normal
		!! The initial text to display in the box, if any
		var string value = empty
		var string prefix = empty
		var string suffix = empty

	Concept File extends Component
		var string value = empty

	Concept Image extends Component
		var string value = empty
		var string defaultValue = empty

	Concept Icon extends Component
		var string alexandriaIcon

	Concept Number extends Component
		var double value = -1
		var string prefix = empty
		var string suffix = empty

	Concept Date extends Component
		var string pattern = "DD/MM/YYYY"
		var word:{FromNow ToNow None} mode = None
		var instant value = empty

	Concept Snackbar extends Component
		var word:{TopCenter TopRight BottomRight BottomCenter BottomLeft TopLeft} position = BottomCenter
		has Component
	Concept Progress extends Component
		Concept Color > var string text; var string background
	Concept Wizard extends Component
		Concept Step > has Component

	Concept Operation extends Component
		var word:{Blank Self Parent Top} target = Self

		sub OpenPage > var Page page
		sub OpenCatalogOperation
		sub OpenDialogOperation
		sub OpenExternalDialogOperation
		sub ExportOperation
		sub ExportSelectionOperation
		sub DownloadOperation
		sub DownloadSelectionOperation
		sub TaskOperation
		sub TaskSelectionOperation
		sub PreviewOperation
		sub SearchOperation

	Concept Header extends Component
		var word:{Fixed Absolute Sticky Static Relative} position = Relative

		has Component

	Concept:Named Grouping extends Component
		var string label

	Concept:Named Sorting extends Component
		var string label
		var boolean visible = true

	Concept Selector extends Component
		has Component

	Concept Frame extends Component
		var Display display

	Concept Column extends Component
		var string label
		has Component