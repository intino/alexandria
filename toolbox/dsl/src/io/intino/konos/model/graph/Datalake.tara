dsl Proteo

use DataTypes

Concept Datamart
	var string path

Concept:{0..1} Datalake
	Concept Domain
	Concept:Named Tank
		var string fullName

Facet TankType on Datalake.Tank
	sub EventTank
		var Schema schema
	sub SetTank
Facet Singleton on Datalake.Tank with SetTank

Facet DatalakeLocation on Datalake is abstract
	sub Remote
		var string url
		var string user
		var string password
		var string domain
		var string clientID
	sub Local
		var string path

Facet Mirror on Datalake with Remote
	var string path
	var integer:{Months} period




Concept:Named Mounter
	has:{1..1} Source

Concept Source is component

Concept Collect is component

Facet MounterType on Mounter is required
	sub Batch
		Concept:{0..1}:Named Filter
		Concept:{0..1} Aggregation
			Concept:{1..*} Column
				var string name
				var word:{Boolean String Integer Double Long} type
	sub Realtime

Facet SourceType on Source is abstract required
	sub Event
	sub Population
		Concept:{1..1} Column
			var string name
			var word:{Boolean String Integer Double} type

Facet Id on Population.Column
	var word:{Boolean String Integer Double Long} type = Long

Facet CollectType on Collect is abstract required
	var Datamart datamart
	sub File > var word:{Arff Tabb Csv} format
	sub Graph

Concept Feeder
	var Datalake.Tank tanks
	has:Named Sensor

Concept:Named Sensor is component

Facet UserSensor on Sensor
	var integer:{%} width = 100%
	var integer:{%} height = 100%

Facet FormEdition extends UserSensor on Sensor
	var string path

Facet DocumentEdition extends UserSensor on Sensor
	var word:{Offline Online} mode = Online

Facet DocumentSignature extends UserSensor on Sensor
	var word:{Sign CoSign CounterSign} signType
	var word:{Pkcs7 XadesAttached XadesDetached CadesAttached CadesDetached} signFormat

Facet Poll extends UserSensor on Sensor
	var string defaultOption = empty
	Concept Option
		var string value
		var Schema event
		has Option

Facet Device on Sensor

Concept EventSource is abstract
	var string name = 'io.intino.konos.model.graph.Utils.isUUID(self.name$()) && self.schema() != null ? self.schema().name$() : self.name$()' is private reactive
	var Schema schema
	var string subdomain = ""
