dsl Proteo

use UI

Concept Dialog extends Component
	var string url
	var string label
	var string description
	var word:{Tabs Wizard} mode = Tabs
	var boolean readonly = false

	Concept:{0..1} Toolbar
		Concept:Named Operation
			var string label
			var boolean closeAfterExecution = true

	Concept Tab
		var string label
		Concept Input
			var string label
			var boolean isRequired = false
			var boolean isReadonly = false
			var string placeHolder = ""
			var string helper = ""
			var string defaultValue = ""
			var string validator = ""
			sub Text
				var word:{Normal Uppercase Lowercase Email Url} edition = Normal
				Concept:{0..1} Validation
					var string[] allowedValues = empty
					var string[] disallowedValues = empty
					var boolean disallowEmptySpaces = false
					var string mask = ""
						//a - Represents an alpha character (A-Z,a-z)
						//9 - Represents a numeric character (0-9)
						//* - Represents an alphanumeric character (A-Z,a-z,0-9)
					Concept:{0..1} Length
						var integer min
						var integer max
			sub Section
				has Input
			sub Memo
				var word:{raw rich} mode = raw
				var integer height = 5
			sub Password
				Concept:{0..1} Validation
					var word[]:{letter number symbol} isRequired = letter number symbol
					Concept:{1..1} Length
						var integer min
						var integer max
			sub OptionBox
				var string[] options = empty
				var string source = ""
				sub RadioBox
				sub CheckBox
					var word:{Boolean List} mode
				sub ComboBox

			sub Resource
				var boolean showPreview = false
				Concept:{0..1} Validation
					var double:{kbytes} maxSize
					var string[] allowedExtensions = empty
				sub File
				sub Picture
			sub Date
				var string format = "dd/MM/yyyy"
			sub DateTime
				var string format = "dd/MM/yyyy HH:mm:ss"


Facet Multiple on Dialog.Tab.Input
	var integer min= -1
	var integer max= -1


