dsl Proteo

use OtherComponents
use OtherComponents
use CatalogComponents
use Services
use Source
use Display
use UI

Concept:Named Component extends Display
	var string color = empty
	var Format[] format = empty
	var boolean visible = true
	Facet Option
	Facet Labeled
		var string label
    	var word:{Top Bottom Left Right} position = Left
	Facet DynamicLoaded
		var word:{VerySlow Slow Fast VeryFast} loadTime = VeryFast
	Facet Traceable
		var integer maxAge = -1
		var boolean secure = false

Concept Block extends Component
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None
	var word:{Never Always IfMobile IfNotMobile} hidden = Never
	has Component

	Facet Drawer
		var word:{Left Top Bottom Right} position = Left
		var word:{Permanent Persistent PersistentAndMini Temporary} variant = Temporary
	Facet Splitter
		var string splitMobileLabel = "Back"
	Facet Absolute extends Absolute
	Facet Relative extends Relative
	Facet AutoSize extends AutoSize
	Facet Paper
	Facet Badge
		var integer value = -1
		var integer max = -1
		var boolean showZero = false
		var word:{Normal Dot} mode = Normal
	Facet Parallax
		var string background
	Facet Conditional
		var Component.Option selected = empty
	Facet Animated extends Animated

	Facet Multiple extends Multiple
	Facet Popover
		var word:{TopLeft TopCenter TopRight LeftTop LeftCenter LeftBottom RightTop RightCenter RightBottom BottomLeft BottomCenter BottomRight} position = BottomCenter

Concept:Named Template extends Component
	var string modelClass = empty
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None
	var boolean scrollable = true
	has Component
	Facet Desktop
		has:{1..1} VisualizationComponents.Header
		//has:{1..1} ChildComponents.Tabs

	Facet DrawerNavigable with Desktop
		var word:{Left Right Top Bottom} direction = Left
		var word:{Normal Modal} mode = Normal
		var boolean hasMiniVariant = false
		var boolean underHeader = false

	Facet Responsible with DrawerNavigable
	Facet Persistent with DrawerNavigable
	Facet Absolute extends Absolute
	Facet Relative extends Relative


Concept InteractionComponents
	Concept Toolbar extends Component
		has Component
		Facet Linked
			var CatalogComponents.Collection to
			has ActionableComponents.Actionable

Concept ActionableComponents
	Concept Actionable extends Component is abstract
		var string title
		var word:{Blank Self Parent Top} target = Self
		var word:{Small Medium Large} size = Medium

		sub Link
		sub AbstractButton
			var word:{None Outline Fill} highlight = None
			sub Button
			sub AbstractSplitButton
				var string[] options
				var string defaultOption
				sub SplitButton
				sub IconSplitButton
					var string icon
				sub MaterialIconSplitButton
					var string icon
			sub IconButton
				var string icon
			sub MaterialIconButton
				var string icon
			sub AvatarIconButton
		sub Switch
			var word:{On Off} state = Off
		sub AbstractToggle
			var word:{On Off} state = Off

			sub Toggle
			sub IconToggle
				var string icon
			sub MaterialIconToggle
				var string icon

		Facet Readonly
		Facet Addressable extends Addressable
		Facet Secured
			sub Affirmed
				var string affirmText
			sub Signed
				var word:{SimplePassword OneTimePassword} mode = SimplePassword
				var string signText
				var string reasonText = empty

		Facet Behavior
			sub Action > var word:{CurrentObject Selection} context = CurrentObject
			sub OpenDrawer > var Block.Drawer drawer
			sub CloseDrawer > var Block.Drawer drawer
			sub OpenPage > var Service.UI.Resource.Page page
			sub OpenSite > var string site
			sub OpenBlock > var Block.Conditional block
			sub CloseBlock > var Block.Conditional block
			sub OpenDialog > var OtherComponents.AbstractDialog dialog
			sub CopyToClipboard > var string text = empty
			sub OpenLayer > var word:{Slide Grow Zoom Fade} transition = Slide
			sub CloseLayer
			sub CloseDialog > var OtherComponents.AbstractDialog dialog
			sub OpenPopover > var Block.Popover popover; var word:{MouseClick MouseOver} triggerEvent = MouseClick
			sub SelectPreviousItem > var CatalogComponents.Collection collection
			sub SelectNextItem > var CatalogComponents.Collection collection
			sub SignText > var string text = empty; var word:{CAdES XAdES} signFormat = XAdES
			sub SignDocument > var string document = empty
			sub Export
				var string[] options = empty
				var instant from = empty
				var instant to = empty
				var instant min = empty
				var instant max = empty
				var integer:{Days} rangeMin = -1 Days
				var integer:{Days} rangeMax = -1 Days
			sub Download
				var string[] options = empty
				var CatalogComponents.Collection collection = empty
				var word:{CurrentObject Selection} context = CurrentObject

Concept:{0..1} VisualizationComponents
	Concept Kpi extends Component
		var word:{Square Circle} mode = Circle
		var string label = empty
		var string unit = ""
		var string backgroundColor = "transparent"
		var string textColor = "black"
		var string highlightedColor = "black"
		var double value
		var word:{Small Medium Large} size = Small
		var word:{Inside Outside} textPosition = Outside

	Concept Header extends Component
		var word:{Fixed Absolute Sticky Static Relative} position = Relative
		var integer elevation = 4
		has Component
		Facet Absolute extends Absolute
		Facet Relative extends Relative

	Concept Chart extends Component
		var Dataframe input
		var string serverUrl = empty
		var word:{Image Html} output = Html
		var string query
		Facet Absolute extends Absolute
		Facet Relative extends Relative

	Concept Dashboard extends Component

		Concept Parameter
			var string name
			var string value

		Facet Absolute extends Absolute
		Facet Relative extends Relative

		Facet Shiny
			var string serverScript
			var string uiScript
			var string[] resources = empty

		Facet Metabase
			var string url
			var string secretKey
			var boolean bordered = true
			var boolean titled = false
			var word:{Dark Light} theme = Light

	Concept AppDirectory extends Component
		var string materialIcon = "Apps"

		Concept:{1..1} Source
			Facet FromResource
				var string path = empty
				var string separator = "\\t"

			Facet FromFile
				var string file = empty
				var string separator = "\\t"

			Facet Inline
				Concept:{1..*} Application
					var string name
					var string url
					Concept Translation
						var string language
						var string name

	Concept Spinner extends Component
		var word:{Bar Circle Hash Ring Rise} mode = Rise
		var integer:{px} size = 0px

	Concept Stepper extends Component
		var word:{Horizontal Vertical} orientation = Horizontal
		var word:{Top Bottom Left Right} position = Bottom
		var word:{Full NoToolbar} style = Full
		Concept Step extends Component
			has Component
			Facet MaterialIcon
				var string icon

	Concept AbstractSlider extends Component
		var word:{Vertical Horizontal} arrangement = Horizontal
		var word:{Full ToolbarOnly SliderOnly NavigationOnly OrdinalSelectorOnly} style = Full
		var word:{SliderTop SliderBottom} position = SliderTop
		Concept:{0..1} Animation
			var integer:{Millis} interval
			var boolean loop = false

		sub Slider
			var long value = -1
			Concept:{1..1} Range
				var integer min
				var integer max

		sub RangeSlider
			var long from = -1
			var long to = -1
			var integer minimumDistance = -1
			Concept:{1..1} Range
				var integer min
				var integer max

		sub TemporalSlider
			var long value = -1
			var CatalogComponents.Collection[] collections
			var word[]:{Year QuarterOfYear Month Week Day SixHours Hour FifteenMinutes Minute Second} scales

			Concept:{1..1} Range
				var instant min
				var instant max
		Facet Readonly

	Concept DocumentEditor extends Component
		var string document = empty

		Facet Absolute extends Absolute
		Facet Relative extends Relative

		Facet Collabora
			var string editorUrl

	Concept DateNavigator extends Component
		var TemporalComponent[] temporalComponents = empty
		var word[]:{Minute Hour Day Week Month Year} scales = Minute Hour Day Week Month Year
		var instant selected = empty

		Concept:{0..1} Range
			var instant from
			var instant to

	Concept TemporalComponent extends Component

		sub Timeline
			var word:{Summary Catalog} mode = Summary

		sub Reel

	Concept Eventline extends Component
		var word:{Vertical Horizontal} arrangement = Horizontal
		var word:{Left Right} toolbarArrangement = Right

Concept HelperComponents
	Concept HelperComponent extends Component

	Concept Row extends HelperComponent
		var CatalogComponents.Moldable.Mold.Item[] items

Facet Editable is abstract
	var string placeholder = ""
	var string defaultValue = ""
	var string validator = ""

Facet Range with Editable, Multiple //TODO est√° correcto?
	var integer min = -1
	var integer max = -1

Facet Absolute
	var integer:{px} width = -1px
	var integer:{px} height = -1px

Facet Relative
	var double:{%} width = 100%
	var double:{%} height = 100%
	var integer:{px} offsetWidth = -1px
	var integer:{px} offsetHeight = -1px

Facet AutoSize

Facet Animated
	var word:{Slide Fade Grow Zoom} mode = Slide
	Concept:{0..1} Transition
		var word:{Left Top Bottom Right} direction = Right
		var integer:{Millis} duration = 500 Millis

Facet Multiple
	var string noItemsMessage = empty
	var boolean wrapItems = true
	var word:{Vertical Horizontal} arrangement = Vertical
	var word:Spacing spacing = None
	var boolean collapsed = false
	Concept:{0..1} Count
		var integer min = 0
		var integer max = -1

Facet Navigable
	var word:{Top Bottom} position = Bottom