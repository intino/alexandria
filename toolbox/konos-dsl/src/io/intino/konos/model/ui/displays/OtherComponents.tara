dsl Proteo

use Services
use CatalogComponents
use Display
use Component

Concept OtherComponents
	Concept Chip extends Component
	Concept Card extends Component
	Concept Tooltip extends Component

	Concept User extends Component
		var word:{OnlyPhoto PhotoWithFullname} mode = PhotoWithFullname
		has Component

	Concept Snackbar extends Component
		var word:{TopCenter TopRight BottomRight BottomCenter BottomLeft TopLeft} position = BottomCenter
		has Component

	Concept Progress extends Component
		Concept Color > var string text; var string background
		Facet Circular
		Facet Linear

	Concept MicroSite extends Component
		var string site
		var word[]:{DownloadMicrosite DownloadContent} downloadOperations = DownloadMicrosite DownloadContent

	Concept HtmlViewer extends Component
		var string content = empty

	Concept BaseStamp extends Component
		Facet Multiple extends Multiple
		Facet Editable extends Editable

		!! Renders template in this component
		sub TemplateStamp
			var Template template

		sub DisplayStamp
			var Display display

		!! Stamps template that belongs to owner project
		sub OwnerTemplateStamp
			var Service.UI.Use owner
			var string template

		!! Stamps template from other app
		sub ProxyStamp
			var string proxy
			var Service.UI.Use from

			Concept Parameter
				var string name
				var string value

	Concept Frame extends Component
		var string url
		Facet Absolute
		Facet Relative

	Concept Selector extends Component
		var boolean multipleSelection = false
		has Component
		Facet Addressable extends Addressable
		Facet Tabs
			var word:{Off On Auto} scrollButtons = Off
		Facet Menu
			var word:{Horizontal Vertical} layout = Vertical
		Facet ListBox
		Facet CollectionBox
			var CatalogComponents.Collection source = empty
			var string placeholder = ""
			var boolean allowOther = false
			has:{0..1} CatalogComponents.Collection
		Facet RadioBox
			!! The value that should initially be selected, defaults to the first value.
			var string selected = empty
			var word:{Horizontal Vertical} layout = Vertical
		Facet CheckBox
			!! The value(s) that should initially be selected. Defaults to the first value, or no values if Multiple facet is present
			var string[] selected = empty
			var word:{Horizontal Vertical} layout = Vertical
		Facet ComboBox
			var string placeholder = ""
			var integer:{px} maxMenuHeight = 300px
			var word:{FieldView TextView} view = FieldView
		Facet ToggleBox
			var word:{Small Medium Large} size = Medium
			var string selected = empty
			var word:{Horizontal Vertical} layout = Horizontal
		Facet Readonly
		Facet Required with RadioBox, CheckBox, ComboBox
		Facet Focused with RadioBox, CheckBox, ComboBox

	Concept BaseIcon extends Component
		var string icon
		Facet Multiple
		sub Icon
		sub MaterialIcon

	Concept AbstractDialog extends Component is abstract
		var string title
		Facet Absolute extends Absolute
		Facet Relative extends Relative
		Facet Animated extends Animated
		Facet FullScreen
		Facet Modal
		sub Dialog
			has Component

		sub AlertDialog
			var string message
			var string closeLabel = "Close"
			var string acceptLabel = empty
		sub DecisionDialog
			has:{1..1} Selector
		sub CollectionDialog
			var boolean allowSearch = true
			has:{0..1} CatalogComponents.Collection

	Concept Divider extends Component