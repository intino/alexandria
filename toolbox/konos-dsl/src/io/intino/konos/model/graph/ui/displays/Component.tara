dsl Proteo

use OtherComponents
use CatalogComponents
use Services
use Source
use Display
use UI

Concept:Named Component extends Display
	var string color = empty
	var Format[] format = empty
	var boolean visible = true
	Aspect Option
	Aspect CollapseTriggerer
    	var Block.Collapsible for
	Aspect Labeled
	Aspect DynamicLoaded
    	var word:{VerySlow Slow Fast VeryFast} loadTime = VeryFast
	Aspect Traceable
		var integer maxAge = -1
		var boolean secure = false

Concept Block extends Component
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None
	var word:{Never Always IfMobile IfNotMobile} hidden = Never
	has Component

	Aspect Drawer
    	var word:{Left Top Bottom Right} position = Left
    	var word:{Permanent Persistent PersistentAndMini Temporary} variant = Temporary
    Aspect Splitter
    	var string splitMobileLabel = "Back"
	Aspect Absolute
	Aspect Relative
	Aspect Paper
    Aspect Badge
    	var integer value = -1
    	var integer max = -1
    	var boolean showZero = false
    	var word:{Normal Dot} mode = Normal
    Aspect Parallax
    	var string background
    Aspect Collapsible
    Aspect Conditional
    	var Component.Option selected = empty
    Aspect Animated
    Aspect Multiple
    Aspect Popover
    	var word:{TopLeft TopCenter TopRight LeftTop LeftCenter LeftBottom RightTop RightCenter RightBottom BottomLeft BottomCenter BottomRight} position = BottomCenter

Concept:Named Template extends Component
	var string modelClass = empty
	var word[]:Layout layout = Vertical
	var word:Spacing spacing = None
	has Component
	Aspect Desktop
    	has:{1..1} OtherComponents.Header
		//has:{1..1} ChildComponents.Tabs

	Aspect DrawerNavigable with Desktop
		var word:{Left Right Top Bottom} direction = Left
		var word:{Normal Modal} mode = Normal
		var boolean hasMiniVariant = false
		var boolean underHeader = false

	Aspect Responsible with DrawerNavigable
	Aspect Persistent with DrawerNavigable
	Aspect Absolute
	Aspect Relative


Concept InteractionComponents

	Concept Toolbar extends Component
		has Component
		Aspect Linked
        	var CatalogComponents.Collection to
        	has Actionable

	Concept Actionable extends Component is abstract
		var string title
		var word:{Blank Self Parent Top} target = Self
		var word:{Small Medium Large} size = Medium

		sub Link
		sub AbstractButton
			var word:{None Outline Fill} highlight = None
			sub Button
			sub SplitButton
				var string[] options
				var string defaultOption
			sub IconButton
				var string icon
			sub MaterialIconButton
				var string icon
			sub AvatarIconButton
		sub Switch
			var word:{On Off} state = Off
		sub AbstractToggle
			var word:{On Off} state = Off

			sub Toggle
			sub IconToggle
				var string icon
			sub MaterialIconToggle
				var string icon

		Aspect Readonly
		Aspect Addressable

		Aspect Secured
			sub Affirmed
				var string affirmText
			sub Signed
				var string signText
				var string reasonText = empty

		Aspect Behavior
			sub Action > var word:{CurrentObject Selection} context = CurrentObject
			sub OpenDrawer > var Block.Drawer drawer
			sub CloseDrawer > var Block.Drawer drawer
			sub OpenPage > var Service.UI.Resource.Page page
			sub OpenSite > var string site
			sub OpenBlock > var Block.Conditional block
			sub OpenDialog > var OtherComponents.AbstractDialog dialog
			sub OpenLayer > var word:{Slide Grow Zoom Fade} transition = Slide
			sub CloseDialog > var OtherComponents.AbstractDialog dialog
			sub OpenPopover > var Block.Popover popover; var word:{MouseClick MouseOver} triggerEvent = MouseClick
			sub SelectPreviousItem > var CatalogComponents.Collection collection
			sub SelectNextItem > var CatalogComponents.Collection collection
			sub Export
				var string[] options = empty
				var instant from = empty
				var instant to = empty
				var instant min = empty
				var instant max = empty
				var integer:{Days} rangeMin = -1 Days
				var integer:{Days} rangeMax = -1 Days
			sub Download
				var string[] options = empty
				var CatalogComponents.Collection collection = empty
				var word:{CurrentObject Selection} context = CurrentObject

Concept:{0..1} VisualizationComponents
	Concept KPI extends Component

	Concept Chart extends Component
		var Dataframe input
		var string serverUrl = empty
		var word:{Image Html} output = Html
		var string query
		Aspect Absolute
		Aspect Relative

	Concept Dashboard extends Component

		Concept Parameter
			var string name
			var string value

		Aspect Absolute
		Aspect Relative

		Aspect Shiny
			var string serverScript
			var string uiScript
			var string[] resources = empty

		Aspect Metabase
			var string url
			var string secretKey
			var boolean bordered = true
			var boolean titled = false
			var word:{Dark Light} theme = Light

	Concept Board extends Component
		var string materialIcon = "Apps"

		Concept:{1..1} Source
			Aspect FromResource
				var string path = empty
				var string separator = "\\t"

			Aspect FromFile
				var string file = empty
				var string separator = "\\t"

			Aspect Inline
				Concept:{1..*} Application
					var string name
					var string url

	Concept Spinner extends Component
		var word:{Bar Circle Hash Ring Rise} mode = Rise
		var integer:{px} size = 0px

	Concept Stepper extends Component
		var word:{Horizontal Vertical} orientation = Horizontal
		var word:{Top Bottom Left Right} position = Bottom
		var word:{Full NoToolbar} style = Full
		Concept Step extends Component
			has Component
			Aspect MaterialIcon
				var string icon

	Concept AbstractSlider extends Component
		var word:{Vertical Horizontal} arrangement = Horizontal
		var word:{Full ToolbarOnly SliderOnly NavigationOnly OrdinalSelectorOnly} style = Full
		var word:{SliderTop SliderBottom} position = SliderTop
		var integer value = -1
		Concept:{0..1} Animation
			var integer:{Millis} interval
			var boolean loop = false

		sub Slider
			Concept:{1..1} Range
				var integer min
				var integer max

		sub TemporalSlider
			var CatalogComponents.Collection[] collections
			var word[]:{Year QuarterOfYear Month Week Day SixHours Hour FifteenMinutes Minute Second} scales

			Concept:{1..1} Range
				var instant min
				var instant max
		Aspect Readonly



Concept HelperComponents
	Concept HelperComponent extends Component

	Concept Row extends HelperComponent
		var CatalogComponents.Collection.Mold.Item[] items

Facet Editable
	var string placeholder = ""
	var string defaultValue = ""
	var string validator = ""

Facet Range with Editable, Multiple //TODO est√° correcto?
	var integer min = -1
	var integer max = -1


Facet Absolute
	var integer:{px} width = -1px
	var integer:{px} height = -1px

Facet Relative
	var double:{%} width = 100%
	var double:{%} height = 100%
	var integer:{px} offsetWidth = -1px
	var integer:{px} offsetHeight = -1px

Facet Animated
	var word:{Slide Fade Grow Zoom} mode = Slide
	Concept:{0..1} Transition
		var word:{Left Top Bottom Right} direction = Right
		var integer:{Millis} duration = 500 Millis

Facet Multiple
	var string noItemsMessage = empty
	var boolean wrapItems = true
	var word:{Vertical Horizontal} arrangement = Vertical
	var word:Spacing spacing = None
	var boolean collapsed = false
	Concept:{0..1} Count
		var integer min = 0
		var integer max = -1

Facet Labeled
	var string label
	var word:{Top Bottom Left Right} position = Left

Facet Navigable
	var word:{Top Bottom} position = Bottom