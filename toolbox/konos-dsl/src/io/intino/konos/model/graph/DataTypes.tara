dsl Proteo

Concept Data is abstract
	Aspect List
	Aspect Type
		var string type is reactive private
		sub Real
			var double defaultValue = empty
			var string type = "Double" is reactive private
		sub Integer
			var integer defaultValue = empty
			var string type = "Integer" is reactive private
		sub LongInteger
			var long defaultValue = empty
			var string type = "Long" is reactive private
		sub Bool
			var boolean defaultValue = empty
			var string type = "Boolean" is reactive private
		sub Text
			var string defaultValue = empty
			var string type = "String" is reactive private
		sub Date
			var string format = "dd/MM/yyyy"
			var string type = "java.util.Date" is reactive private
		sub DateTime
			var string format = "dd/MM/yyyy HH:mm:ss"
			var string type = "java.time.Instant" is reactive private
		sub Word
			var string type = "java.lang.Enum" is reactive private
			var string[] values;
		sub File
			var string type = "io.intino.alexandria.Resource" is reactive private
			var word:{body path form header query} in = form
		sub Object
			var Schema schema
			var string type = @schemaName is reactive private
			var boolean isComponent = @isComponent is reactive private
		sub Map
			var string type = "java.util.Map" is reactive private
			Concept:{1..1}:RequiresAspect Key extends Data
			Concept:{1..1}:RequiresAspect Value extends Data


Concept:Named Parameter extends Data is component
	var boolean isRequired = false //TODO si el parametro es en path por defecto es requerido, para el resto de casos por defecto false

!! Schemas allows to define how objects are. This objects can be provided as either parameter or response
Concept Schema
	var boolean isRequired = false
	var boolean multiple = true
	!! Attribute of the schema
	Concept:Named:RequiresAspect Attribute extends Data
		var boolean isRequired = true
	has Schema
	Aspect ExtensionOf
    	var Schema parent

Concept Exception extends Data
	var word:ExceptionCodes code
	var string description = ""

Concept Response extends Data is component
	var string code = "200"
	var string description = ""
	var word:{html xml text} dataFormat = html

Concept Redirect extends Response

//
//Concept Message extends Schema
//	has Message