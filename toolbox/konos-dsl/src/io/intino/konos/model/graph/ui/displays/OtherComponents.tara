dsl Proteo

use Services
use CatalogComponents
use Display
use Component

Concept OtherComponents
	Concept Chip extends Component
	Concept Card extends Component
	Concept Tooltip extends Component

	Concept User extends Component
		var word:{OnlyPhoto PhotoWithFullname} mode = PhotoWithFullname
		has Component

	Concept Snackbar extends Component
		var word:{TopCenter TopRight BottomRight BottomCenter BottomLeft TopLeft} position = BottomCenter
		has Component

	Concept Progress extends Component
		Concept Color > var string text; var string background
		Aspect Circular
        Aspect Linear

	Concept BaseStamp extends Component
		Aspect Multiple
		Aspect Editable

		!! Renders template in this component
		sub TemplateStamp
			var Template template

		sub DisplayStamp
			var Display display

		!! Stamps template that belongs to owner project
		sub OwnerTemplateStamp
			var Service.UI.Use owner
			var string template

		!! Stamps template from other app
		sub ProxyStamp
			var string proxy
			var Service.UI.Use from

			Concept Parameter
				var string name
				var string value

	Concept Frame extends Component
		var string url
		Aspect Absolute
		Aspect Relative

	Concept Header extends Component
		var word:{Fixed Absolute Sticky Static Relative} position = Relative
		var integer elevation = 4
		has Component
		Aspect Absolute
		Aspect Relative

	Concept Selector extends Component
		var boolean multipleSelection = false
		has Component
		Aspect Addressable
		Aspect Tabs
        Aspect Menu
			var word:{Horizontal Vertical} layout = Vertical
        Aspect ListBox
        Aspect CollectionBox
        	var CatalogComponents.Collection source = empty
        	var string placeholder = ""
			has:{0..1} CatalogComponents.Collection
        Aspect RadioBox
        	!! The value that should initially be selected, defaults to the first value.
        	var string selected = empty
        	var word:{Horizontal Vertical} layout = Vertical
        Aspect CheckBox
        	!! The value(s) that should initially be selected. Defaults to the first value, or no values if Multiple facet is present
        	var string[] selected = empty
        	var word:{Horizontal Vertical} layout = Vertical
        Aspect ComboBox
        	var string placeholder = ""
        Aspect ToggleBox
        	var word:{Small Medium Large} size = Medium
        	var string selected = empty
        	var word:{Horizontal Vertical} layout = Horizontal
        Aspect Readonly
        Aspect Required with RadioBox, CheckBox, ComboBox
        Aspect Focused with RadioBox, CheckBox, ComboBox

	Concept BaseIcon extends Component
		var string icon
		Aspect Multiple
		sub Icon
		sub MaterialIcon


	Concept AbstractDialog extends Component is abstract
		var string title
		Aspect Absolute
		Aspect Relative
		Aspect Animated
		Aspect FullScreen
		Aspect Modal
		sub Dialog
			has Component

		sub AlertDialog
			var string message
			var string closeLabel = "Close"
			var string acceptLabel = empty
		sub DecisionDialog
			has:{1..1} Selector
		sub CollectionDialog
			var boolean allowSearch = true
			has:{0..1} CatalogComponents.Collection

	Concept Divider extends Component