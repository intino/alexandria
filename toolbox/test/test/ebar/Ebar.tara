dsl Konos

Client (clientID = "{subscriberId}", domain = "ebar") as Ness(url = "{ness_url}", user = "{ness_user}", password = "{ness_password}")
	Mounter(SensorChanged) sensorChanged
	Mounter(StationDisconnected) stationDisconnected
	Mounter(StationAdded) stationAdded
	Mounter(StationRemoved) stationRemoved
	Mounter(StationModified) stationModified
	Mounter(subdomain = "configuration", schema = AlertModified) alertModified

Schema AlertModified
	Attribute alert as Text
	Attribute active as Bool
	Attribute mailingList as Text List
	Attribute applyToAllStations as Bool

Schema SensorChanged
	Attribute station as Text
	Attribute sensor as Text
	Attribute value as Bool
	Attribute ts as DateTime

Schema StationDisconnected
	Attribute station as Text
	Attribute ts as DateTime

Schema StationAdded
	Attribute name as Text
	Attribute label as Text
	Attribute coordinates as Text
	Attribute ts as DateTime

Schema StationRemoved
	Attribute name as Text
	Attribute ts as DateTime

Schema StationModified
	Attribute name as Text
	Attribute newLabel as Text
	Attribute ts as DateTime


Task CheckSequences as CronTrigger(pattern = "0 0 12 1/1 * ? *", "Every day at 12")
Task CheckEnginesAndAlarmsDuration as CronTrigger(pattern = "0 0/15 * 1/1 * ? *", "Every fifteen minutes")

Service(title = "Smart Port", favicon = "") productUi as UI(port = "9000")
	Authentication(by = "{federationUrl}")
	Resource("/") homePage as DisplayPage(ebarDesktop) Confidential
	Resource("/configuration/alert/:name") configurationPage as DialogPage(editStationAlerts)

Dialog(label = "Editar alertas", description = "alertas de la estación ::station::", url="") editStationAlerts
	Tab("información")
		CheckBox("activar alertas", mode=Boolean) alertActive
		Text("direcciones de correo donde notificar", edition=Email) alertEmails as Multiple
		CheckBox("aplicar al resto de estaciones", mode=Boolean) applyToOtherStations


Panel(layout = Tab, title = "Smart Port", subTitle = "Puerto de Las Palmas", logoPath = "/images/logo.png", faviconPath = "") ebarDesktop as Desktop
	Views
		View(label = "infrastructura") infrastructura as CatalogContainer(infrastructureCatalog)
		View(label = "episodios") episodios as CatalogContainer(sequenceCatalog)
		View(label = "análisis") analisis as DisplayContainer("ebarOlap")
		View(label = "configuración") configuracion as CatalogContainer(configurationCatalog)

Catalog(itemClass = "com.monentia.ebar.graph.Asset", label = "Infrastructure") infrastructureCatalog
	Views views
		View("mapa", mold = infrastructureMold) as MapContainer
			Zoom(14,min=1,max=18)
			Center(latitude = 28.146773457066104, longitude = -15.418557420532238)

Panel ebarOlapRemove
	Views v > View(label = "") olap as DisplayContainer("ebarOlap")


Mold(modelClass = "com.monentia.ebar.graph.Asset") infrastructureMold
	Block
		Block > Location coordinates
		Block
			Block
				Title title
				Description(label = "Estado") lastEpisode
				CatalogLink(catalog = sequenceCatalog) stationSequences
					defaultStyle = "color: white;font-size: 14px;border-radius: 5px;background-color: #365c80;";
					filtered = true

			Block(layout = Horizontal)
				Picture(label = "chart", defaultPicture = empty) chart
					defaultStyle = "margin-top:6px;max-height: 175px;"
				Snippet(label = "sequencia", layout = Flexible, defaultStyle = "height:155px;overflow:auto;margin-top:10px;margin-bottom:10px;") sequence
			Block > Snippet stats

TemporalCatalog(label = "Episodios", itemClass = "com.monentia.ebar.graph.StationSequence", scales = Day, type = Range) sequenceCatalog
	Views views > View("listado", mold = sequenceMold) as ListContainer
	Toolbar > Export(title = "Descarga los episodios del último mes", mode = Icon, polymerIcon = "archive")
	Content
		Sorting(label = "fecha") date
		Grouping(label = "Estación") stations
		Grouping("Estado del episodio") stationSequenceStates
		Grouping("Tipo de episodio") stationSequenceTypes

Mold(modelClass = "com.monentia.ebar.graph.StationSequence") sequenceMold
	Block(layout = Horizontal Center Wrap)
		Block(layout = Horizontal Center)
			Icon(source = Polymer)  icon > hasCustomStyle = true;
			Title(hasCustomStyle = true) label
		Block(layout = Horizontal Flexible EndJustified)
			hiddenIfMobile = true
			Highlight sequenceTime
			Highlight(label = "duration") duration
			Highlight engineADuration
			Highlight engineBDuration
			Highlight failuresCount
	Block(layout = Horizontal Wrap, mode = Expanded)
		Picture("sequencia", defaultPicture = empty, defaultStyle = "margin-top:6px;") chart
		Snippet("sequencia") sequence
			layout = Flexible
			defaultStyle = "height:270px;overflow:auto;margin-top:10px;margin-bottom:10px;"

Catalog("Análisis", itemClass = "com.monentia.ebar.graph.Asset") analysisCatalog
	Views > View(label = "Olap") view as DisplayContainer("ebarOlap")

Catalog("Configuración", itemClass = "com.monentia.ebar.graph.Alerts.Alert") configurationCatalog
	Views views > View("alertas", mold = configurationMold) as ListContainer

Mold(modelClass = "com.monentia.ebar.graph.Alerts.Alert") configurationMold
	Block(Horizontal)
		Block > Icon(source = Polymer) alertIcon
		Block(Flexible)
			Title("estación") title
			Description("direcciones de correo") emails
			Highlight("estado") estado
	Block(Horizontal Wrap, mode = Expanded)
		Block(Flexible) > OpenDialogOperation("editar...", dialog = editStationAlerts,defaultStyle = "margin-left:32px;") editAlert